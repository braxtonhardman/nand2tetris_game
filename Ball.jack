//This is the ball class for drawing the balls to hit the person
//05/04/2025

class Ball
{
    field int x, y, vx, vy, ballR; //basics for the ball height and speed and what not
    field int gravity; //gravity constants to work with our parabolic eqautions
    field boolean right, ballStop; //used to see what direction we need to go for x and when to stop the ball
    field int prevX, prevY; //used to keep track of the previous ball
    
    //Make the constructor for the ball
    constructor Ball new(int startX, int startY, int xPow, int yPow, boolean rightIn)
    {
        // initialize all the fields correctly
        let x = startX;
        let y = startY;
        let gravity = 2;
        let ballR = 5;
        let right = rightIn;
        let ballStop = false;

        //initalize the prevX and Y for later calculations
        let prevX = 0;
        let prevY = 0;

        //Now set the values for how we are gonna calculate the ball movement
       if(right) //player is shooting right (adjust x velocity)
       {
            let vx = xPow;
       }
       else //player is shooting left so move x-pow to negative
       {
            let vx = -xPow;
       }

       //Now since we're dealing with pixel space closer to the ground means a positive y so we're gonan invert the y-value so the ball can go "up" in the user perspective
       let vy = -yPow;

        return this;
    }   


    //Now create the move function to calculate the ball's next x and y address
    method void move()
    {
        //variable declarations
        var int diameter; //diameter for the ball based on radius to use for boundry calculations

        if(~ballStop)
        {
            let diameter = Math.multiply(ballR,2);
            let prevX = x;
            let prevY = y;

            //Update the position and then apply the gravity to vertical velocity (over time)
            let x = x + vx;
            let y = y + vy;
            let vy = vy + gravity;

            //check and make sure that X can't go above or below the memory mapp; Since diameter
            if(x < (0 + diameter))
            {
                let x = (0 + diameter);
            }
            if(x > (511 - diameter))
            {
                let x = (511 - diameter);
            }

            //Now do the same thing for Y
            if(y < (0 + diameter))
            {
                let y = (0 + diameter);
                let vy = 0; //hit the ceiling stop the ball
            }
            if (y > (255 - diameter))
            {
                let y = (255 - diameter);
                let vy = 0; //ball is on the ground
                let vx = 0;
                let ballStop = true; //make sure the ball doesn't move any more
            }
        }
        do animateToNewBall();

        return;
    }

    method void animateToNewBall() //this method animates the ball to the X value
    {
        //variables used to do the calculation
        var int dx, dy, drawX, drawY, steps, i, work;

        //Now update ball animation and movement for smoother motion
        let dx = x - prevX; //deltaX
        let dy = y - prevY; //deltaY

        if(Math.abs(dx) > Math.abs(dy)) //determine if there is more change in the x or y for our animation
        {
            let steps = Math.abs(dx);
        }
        else
        {
            let steps = Math.abs(dy);
        }

        let i = 0;
        while(i < steps) //loop over the number of steps to reanimate the change in that we covered
        {
            //uses linear interpolation equation to calculate the change. prevX = prevX + (dx * i) / steps
            //drawX and drawY are abirtray values to track each time we draw and animate, also so that we don't override prevX and X
            let drawX = prevX + Math.divide(Math.multiply(dx, i), steps);
            let drawY = prevY + Math.divide(Math.multiply(dy, i), steps);

            //now draw the balls
            do Screen.setColor(true);
            do Screen.drawCircle(drawX,drawY,ballR);

            //Make the computer do work so the balls can show up on screen
            let work = 0;
            while(work < 1000) 
            { 
                let work = work + 1; 
            }

            do Screen.setColor(false);
            do Screen.drawCircle(drawX,drawY,ballR);
            let i = i + 1;
        }

        return;
    }

    method void explodeBall() //this method is called to make the ball explode if the player is hit
    {
        //create the variables needed
        var int i, size, drawX, drawY, drawY, work, rectX, rectY;

        //first we're gonna expand the ball like a bomb 3 times before it explodes
        let i = 0;
        while(i < 3) {

            //calculate size and check boudries
            let size = ballR + i * 3;
            if (x < (0 + size)) //far left
            {
                let x = (0 + size);
            }
            if (x > (511 - size)) //far right
            {
                let x = (511 - size);
            }
            if (y < (0 + size)) //top
            {
                let y = (0 + size);
            }
            if (y > (255 - size)) //bottom
            {
                let y = (255 - size);
            }
            
            //Draw the ball on the screen
            do Screen.setColor(true);
            do Screen.drawCircle(x, y, size);

            //make the processor do work
            let work = 0;
            while(work < 5000) {
                let work = work + 1;
            }

            //erase the ball and then increment
            do Screen.setColor(false);
            do Screen.drawCircle(x, y, size);
            let i = i + 1;
        }

        //makes the ball explode into multiple different pieces (like rectangles)
        let i = 0;
        while(i < 4) { //this time we loop over 4 times to help crete the explosion effect

            //calculate the rectangles (exploded shrapnel)
            if(i = 0) { //shoot them up
                let rectX = x;
                let rectY = y - 10;
            }
            if(i = 1) { //shoot them down
                let rectX = x;
                let rectY = y + 10;
            }
            if(i = 2) { //shoot them left
                let rectX = x - 10;
                let rectY = y;
            }
            if(i = 3) { //shoot them right
                let rectX = x + 10;
                let rectY = y;
            }

            //Now check the rectangles boundries
            if (rectX < 0) //far left
            {
                let rectX = 0;
            }
            if (rectX > 507) //far right (507 since our things are 4 pixels) 
            {
                let rectX = 507;
            }
            if (rectY < 0) //top
            {
                let rectY = 0;
            }
            if (rectY > 251) //bottom (same thing as the x 4 pixels)
            {
                let rectY = 251;
            }

            //Draw the rectangles
            do Screen.setColor(true);
            do Screen.drawRectangle(rectX, rectY, rectX + 4, rectY + 4);

            //do some work
            let work = 0;
            while(work < 5000) {
                let work = work + 1;
            }

            //then erase and increment
            do Screen.setColor(false);
            do Screen.drawRectangle(rectX, rectY, rectX + 4, rectY + 4);
            let i = i + 1;
        } 
        return;      
    }

    //now do the draw method for our ball
    method void draw()
    {
        do Screen.setColor(true);
        do Screen.drawCircle(x,y,ballR);
        return;
    }

    method void clearBall() //erase the ball
    {
        do Screen.setColor(false); //basically setting the color to white
        do Screen.drawCircle(x,y,ballR);
        return;
    }

    //couple accessors and mutators for the current x & y of the ball
    method void setX(int xIn)
    {
        let x = xIn;
        return;
    }

    // Set the y position 
    method void setY(int yIn)
    {
        let y = yIn;
        return;
    }

    // Set velocity X 
    method void setVX(int vxIn)
    {
        let vx = vxIn;
        return;
    }

    // Set velocity y 
    method void setVY(int vyIn)
    {
        let vy = vyIn;
        return;
    }

    // Get x position 
    method int getX()
    {
        return x;
    }

    // Get y positon
    method int getY()
    {
        return y;
    }

    // Get velocity x 
    method int getVX()
    {
        return vx;
    }

    // Get velocity y 
    method int getVY()
    {
        return vy;
    }

    //get whether or not the ball is stopped
    method boolean getBallStop() 
    {
        return ballStop;
    }

    // grab the radius of the ball
    method int getBallR()
    {
        return ballR;
    }
}